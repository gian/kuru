(* Based on peg-bootstrap by Kragen Javier Sitaker *)
(* http://github.com/kragen/peg-bootstrap *)
(* Ported to Kuru by Gian Perrone *)
(* http://www.kuru-lang.org *)

fun kupeg_join l = String.concatWith "" l

%start sentence
%empty "\"\""
%result string

datatype ast = 
   Rule of string * ast
 | Choice of ast * ast
 | Sequence of ast * ast
 | Literal of string
 | Nonterm of string
 | Negation of ast
 | Result of string
 | Label of string * ast
 | Star of ast
 | Plus of ast
 | Null

(*type res_rule = ast option 
type res_sp = string option 
type res__ = string option 
type res_sentence = ast list option 
type res_meta = string option 
type res_name = string option 
type res_namechar = string option 
type res_termfrag = ast option 
type res_term = ast option 
type res_nonterminal = ast option 
type res_labeled = ast option 
type res_sequence = ast option 
type res_string = ast option 
type res_stringcontents = string option 
type res_choice = ast option 
type res_negation = ast option 
type res_result_expression = ast option 
type res_expr = string option 
type res_exprcontents = string option 
type res_parenthesized = ast option
fun notNone (NONE : 'a option) = false | notNone (SOME _) = true
fun $ f = valOf f*)

%nonterm sp of string
%nonterm _ of string
%nonterm rule of ast
%nonterm sentence of ast list
%nonterm meta of string
%nonterm name of string
%nonterm namechar of string
%nonterm termfrag of ast
%nonterm term of ast
%nonterm nonterminal of ast
%nonterm labeled of ast
%nonterm sequence of ast
%nonterm string of ast
%nonterm stringcontents of string
%nonterm choice of ast
%nonterm negation of ast
%nonterm result_expression of ast
%nonterm expr of string
%nonterm exprcontents of string
%nonterm parenthesized of ast

val kplineNum = ref 0 
val kpfileName = ref "__dummy__"

%%
sp <- " " / "\n" / "\t".
_  <- sp _ / .

rule    <- n: name _ "<-"_ body: choice "."_ ->
               ((Rule($n,$body)))
           .
sentence <- _ r: rule g: sentence -> ($r :: $g)
          / _ r: rule -> ($r :: [])
          .

meta     <- "!" / "\"" / "<-" / "/" / "." / "(" / ")" /
            ":" / "->" / "*" / "+".
name     <- c: namechar n: name -> ($c ^ $n) / namechar.
namechar <- !meta !sp char.
termfrag <- labeled / nonterminal / string / negation / parenthesized.
term <- t: termfrag 
     ("*" -> (Star($t))
    / "+" -> (Plus($t))
    / -> ($t)).

nonterminal <- n: name _ ->
                   (Nonterm ($n))
             .

labeled <- label: name _ ":"_ value: term ->
               (Label($label,$value))
         .

sequence <- foo: term  bar: sequence -> 
                   (Sequence($foo, $bar))
          / result_expression 
          / -> (Null)
          .
string <- "\"" s: stringcontents "\""_ ->
             (Literal ($s))
        .

stringcontents <-   !"\\" !"\"" c: char  s: stringcontents -> 
                    ($c ^ $s)
                / b: "\\"       c: char  s: stringcontents -> 
                    ($b ^ $c ^ $s)
                / -> ("").
choice <- a: sequence "/"_  b: choice ->
                  (Choice($a,$b))
              / sequence.
negation <- "!"_ t: term ->
                (Negation ($t))
          .
result_expression <- "->"_ result: expr _ ->
                         (Result ($result))
                   .
expr         <- "("_ e: exprcontents ")" -> ("(" ^ $e ^ ")").
exprcontents <- c: (!"(" !")" char / expr)  e: exprcontents -> 
                                            ($c ^ $e)
              / -> ("").
parenthesized <- "("_ body: choice ")"_ -> (($body)).

